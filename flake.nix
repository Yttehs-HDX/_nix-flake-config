{
  description = "Shetty Yttehs's NixOS flake";

  inputs = {
    # Nix packages
    nixpkgs = { url = "github:nixos/nixpkgs?ref=nixos-25.11"; };

    # Home Manager (for managing users)
    home-manager = {
      url = "github:nix-community/home-manager/release-25.11";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # Nix user repository (NUR)
    nur = {
      url = "github:nix-community/NUR";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # Hexecute (Custom utility for magic power)
    hexecute = {
      url = "github:ThatOtherAndrew/Hexecute";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # OpenClaw (AI coding agent)
    openclaw = {
      url = "github:openclaw/nix-openclaw";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    # NixVim (Custom NixOS Vim configuration)
    nixvim = {
      url = "github:nix-community/nixvim/nixos-25.11";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs = { nixpkgs, nur, home-manager, hexecute, openclaw, nixvim, ... }:
    let
      lib = nixpkgs.lib;
      hostRegistry = import ./hosts;
      userRegistry = import ./users;
      profiles = {
        hosts = lib.mapAttrs (_: entry: entry.profile) hostRegistry;
        users = userRegistry;
      };
      hostNames = builtins.attrNames hostRegistry;
      system = import ./system {
        inherit lib nur home-manager hexecute openclaw nixvim;
      };
    in {
      nixosConfigurations = lib.genAttrs hostNames
        (hostname: system { inherit hostname profiles; });
    };
}
